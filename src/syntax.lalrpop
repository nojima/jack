use compact_str::CompactString;
use crate::ast::{Expr, UnaryOp, BinaryOp};
use crate::lexer;
use crate::token::Token;
use std::rc::Rc;

grammar;

//-----------------------------------------------------------------------------

CommaSeparated<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

//-----------------------------------------------------------------------------

pub Expr: Expr =  {
    OrExpr,
    IfExpr,
    LocalExpr,
    Function,
};

IfExpr: Expr =
    "if" <cond:Expr> "then" <then:Expr> "else" <else_:Expr>
        => Expr::If(Box::new(cond), Box::new(then), Box::new(else_));

LocalExpr: Expr =
    "local" <name:Identifier> <params:("(" <CommaSeparated<Identifier>> ")")?> "=" <expr1:Expr> ";" <expr2:Expr>
        => {
            match params {
                None => Expr::Local(name, Box::new(expr1), Box::new(expr2)),
                Some(params) => {
                    // Desuger: local <name>(<params>) = <expr1>; <expr2>
                    //       => local <name> = function(<params>) <expr1>; <expr2>
                    let func = Expr::Function(params, Box::new(expr1));
                    Expr::Local(name, Box::new(func), Box::new(expr2))
                }
            } 
        };

OrExpr: Expr = {
    AndExpr,
    <lhs:OrExpr> "||" <rhs:AndExpr>
        => Expr::BinaryOp(BinaryOp::Or, Box::new(lhs), Box::new(rhs)),
}

AndExpr: Expr = {
    CompExpr,
    <lhs:AndExpr> "&&" <rhs:CompExpr>
        => Expr::BinaryOp(BinaryOp::And, Box::new(lhs), Box::new(rhs)),
}

CompExpr: Expr = {
    AddExpr,
    <lhs:AddExpr> <op:CompOp> <rhs:AddExpr>
        => Expr::BinaryOp(op, Box::new(lhs), Box::new(rhs)),
}

CompOp: BinaryOp = {
    "==" => BinaryOp::Eq,
    "!=" => BinaryOp::NotEq,
};

AddExpr: Expr = {
    MulExpr,
    <lhs:AddExpr> <op:AddOp> <rhs:MulExpr>
        => Expr::BinaryOp(op, Box::new(lhs), Box::new(rhs)),
};

AddOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
};

MulExpr: Expr = {
    UnaryExpr,
    <lhs:MulExpr> <op:MulOp> <rhs:UnaryExpr>
        => Expr::BinaryOp(op, Box::new(lhs), Box::new(rhs)),
};

MulOp: BinaryOp = {
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
    "%" => BinaryOp::Mod,
};

UnaryExpr: Expr = {
    PostfixExpr,
    "-" <PostfixExpr> => Expr::UnaryOp(UnaryOp::Neg, Box::new(<>)),
};

PostfixExpr: Expr = {
    Term,
    FunctionCall,
    FieldAccess,
    IndexAccess,
};

FunctionCall: Expr =
    <func:PostfixExpr> "(" <args:CommaSeparated<Expr>> ")"
        => Expr::FunctionCall(Box::new(func), args);

FieldAccess: Expr =
    <expr:PostfixExpr> "." <name:Identifier>
        => Expr::FieldAccess(Box::new(expr), name);

IndexAccess: Expr =
    <expr:PostfixExpr> "[" <index:Expr> "]"
        => Expr::IndexAccess(Box::new(expr), Box::new(index));

Term: Expr = {
    Literal,
    Variable,
    "(" <Expr> ")",
};

Variable: Expr =
    Identifier => Expr::Variable(<>);

Literal: Expr = {
    "true"  => Expr::Bool(true),
    "false" => Expr::Bool(false),
    "null"  => Expr::Null,
    Number  => Expr::Number(<>),
    String  => Expr::String(Rc::new(<>)),
    Array   => Expr::Array(<>),
    Dict    => Expr::Dict(<>),
};

Number: f64 = "number literal";

String: String = "string literal";

Identifier: CompactString = "identifier";

Array: Vec<Expr> =
    "[" <CommaSeparated<Expr>> "]";

Dict: Vec<(CompactString, Expr)> =
    "{" <CommaSeparated<KeyValue>> "}";

KeyValue: (CompactString, Expr) =
    <k:Key> ":" <v:Expr> => (k.into(), v);

Key: CompactString = {
    String => <>.into(),
    Identifier,
};

Function: Expr =
    "function" "(" <params:CommaSeparated<Identifier>> ")" <expr:Expr>
        => Expr::Function(params, Box::new(expr));
 
extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum Token {
        "true"  => Token::True,
        "false" => Token::False,
        "null"  => Token::Null,

        "if" => Token::If,
        "then" => Token::Then,
        "else" => Token::Else,
        "local" => Token::Local,
        "function" => Token::Function,

        "number literal" => Token::Number(<f64>),
        "string literal" => Token::String(<String>),
        "identifier"     => Token::Identifier(<CompactString>),

        "." => Token::Dot,
        ":" => Token::Colon,
        ";" => Token::Semicolon,
        "," => Token::Comma,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "[" => Token::LBracket,
        "]" => Token::RBracket,
        "{" => Token::LBrace,
        "}" => Token::RBrace,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "*" => Token::Asterisk,
        "/" => Token::Slash,
        "%" => Token::Percent,
        "=" => Token::Eq,
        "==" => Token::EqEq,
        "!" => Token::Exclamation,
        "!=" => Token::NotEq,
        "&" => Token::Ampersand,
        "&&" => Token::AndAnd,
        "|" => Token::Pipe,
        "||" => Token::OrOr,
    }
}
